SUBROUTINE GSTATS_PRINT(KULOUT,PAVEAVE,KLEN)

!**** *GSTATS_PRINT* - print timing statistics

!     PURPOSE.
!     --------
!       To print out timings gathered by GSTATS


!**   INTERFACE.
!     ----------
!       *CALL* *GSTATS_PRINT*

!        EXPLICIT ARGUMENTS     None
!        --------------------


!        IMPLICIT ARGUMENTS
!        --------------------
!        Module YOMSTATS

!     METHOD.
!     -------


!     EXTERNALS.   
!     ----------   

!     REFERENCE.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     AUTHOR.
!     -------
!        Mats Hamrud ECMWF

!     MODIFICATIONS.
!     --------------
!        ORIGINAL : 98-11-15
!        D.Salmond: 99-09-21 : Timer for SLCOMM2
!     ------------------------------------------------------------------
#include "tsmbkind.h"

USE YOMGSTATS
USE YOMMPI   , ONLY : MREALT
USE MPL_MODULE

IMPLICIT NONE

INTEGER_M :: KULOUT,KLEN
REAL_B :: PAVEAVE(0:KLEN)
CHARACTER*7  CLACTION(0:3)
INTEGER_M,PARAMETER :: JPARRAYS=8
REAL_B :: ZREABUF(JPARRAYS*(JPMAXSTAT+1))
REAL_B :: ZAVEAVE(0:JPMAXSTAT),ZAVEMAX(0:JPMAXSTAT),ZTIMELCALL(0:JPMAXSTAT),&
         &ZTHISTIME(0:JPMAXSTAT)
REAL_B :: T_SUM

!     LOCAL INTEGER SCALARS
INTEGER_M :: ICALLS, IERR, ILBUF, ILSEND, &
             &ISEND, ITAG, JJ, JNUM, JROC, JCALL, ICALLER,IACTION

!     LOCAL REAL SCALARS
REAL_B :: ZAVE, ZAVETCPU, ZAVEVCPU, ZCOMTIM, ZDETAIL,&
          &ZFRAC, ZMAX, ZMEAN, ZSTDDEV, ZSUM, ZSUMB, &
          &ZTOTAL, ZTOTCPU, ZTOTMEAN, ZTOTUNBAL, ZTOTVCPU, &
          &ZUNBAL

!     ------------------------------------------------------------------


ILBUF = JPARRAYS*(JPMAXSTAT+1)
ZAVEAVE(:) = _ZERO_
ZAVEMAX(:) = _ZERO_

WRITE(KULOUT,'(A)')'===-=== START OF TIMING STATISTICS ===-==='
IF(LSYNCSTATS.AND.NPROC_STATS>1) THEN
  WRITE(KULOUT,'(A)')'START OF TIMINGS SYNCRONIZED'
ENDIF
IF (LSTATS .AND. MYPROC_STATS /= 1) THEN
  JJ = 1
  DO JNUM=0,JPMAXSTAT
    ZREABUF(JJ  ) = TIMESUM(JNUM)
    ZREABUF(JJ+1) = TIMESQSUM(JNUM)
    ZREABUF(JJ+2) = TIMEMAX(JNUM)
    ZREABUF(JJ+3) = TIMESUMB(JNUM)
    ZREABUF(JJ+4) = TIMELCALL(JNUM)
    ZREABUF(JJ+5) = NCALLS(JNUM)
    ZREABUF(JJ+6) = TTCPUSUM(JNUM)
    ZREABUF(JJ+7) = TVCPUSUM(JNUM)
    JJ = JJ+JPARRAYS
  ENDDO
  ILSEND = ILBUF
  ISEND =1
  ITAG = JPTAGSTAT
  CALL MPL_SEND(ZREABUF(1:ILSEND),KDEST=NPRCIDS_STATS(ISEND), &
       & KTAG=ITAG,CDSTRING='GSTATS_PRINT:')
ELSEIF(LSTATS) THEN
  DO JROC=1,NPROC_STATS
    IF (JROC /= 1) THEN
      ITAG = JPTAGSTAT
      CALL MPL_RECV(ZREABUF(1:ILBUF),KSOURCE=NPRCIDS_STATS(JROC), &
       & KTAG=ITAG,CDSTRING='GSTATS_PRINT:')
      JJ = 1
      DO JNUM=0,JPMAXSTAT
        TIMESUM(JNUM)   = ZREABUF(JJ  )
        TIMESQSUM(JNUM) = ZREABUF(JJ+1)
        TIMEMAX(JNUM)   = ZREABUF(JJ+2)
        TIMESUMB(JNUM)  = ZREABUF(JJ+3)
        TIMELCALL(JNUM) = ZREABUF(JJ+4)
        NCALLS(JNUM)    = NINT(ZREABUF(JJ+5))
        TTCPUSUM(JNUM)  = ZREABUF(JJ+6)
        TVCPUSUM(JNUM)  = ZREABUF(JJ+7)
        JJ = JJ+JPARRAYS
      ENDDO
    ENDIF
    IF (JROC == 1) THEN
      ZTOTAL=TIMESUM(0)
      ZTOTCPU = TTCPUSUM(0)
      ZTOTVCPU = TVCPUSUM(0)
    ENDIF
    IF(.NOT. LSTATSCPU) THEN
      TTCPUSUM(1:JPMAXSTAT) = -_ZERO_
      TVCPUSUM(1:JPMAXSTAT) = -_ZERO_
    ENDIF
    IF( LDETAILED_STATS .OR. NPROC_STATS==1) THEN
      WRITE(KULOUT,*) 'TIMING STATISTICS:PROCESSOR=',JROC
      IF(NPROC_STATS > 1) THEN
        WRITE(KULOUT,'(A,F10.1,A)')'STARTUP COST ',&
         &TIME_START(JROC),' SECONDS'
      ENDIF
      WRITE(KULOUT,*)&
       &'NUM, CALLS, SUM(s),AVE(ms),CPUAVE(ms),VAVE(ms),'//&
       &'STDDEV(ms),MAX(ms)'//&
       &',SUMB(s),FRAC(%)'
    ENDIF
    DO JNUM=0,JPMAXSTAT
      IF(NCALLS(JNUM) > 1) THEN
        ICALLS = NCALLS(JNUM)/2
        ZSUM = TIMESUM(JNUM)
        ZAVE = TIMESUM(JNUM)/ICALLS*1000._JPRB
        ZMAX = TIMEMAX(JNUM)*1000._JPRB
        ZSUMB = TIMESUMB(JNUM)
        ZFRAC = TIMESUM(JNUM)/ZTOTAL*100.0_JPRB
        ZAVEAVE(JNUM)=ZAVEAVE(JNUM)+ZAVE
        ZAVEMAX(JNUM)=MAX(ZAVEMAX(JNUM),ZAVE)
        ZAVETCPU = TTCPUSUM(JNUM)/ICALLS*1000._JPRB
        ZAVEVCPU = TVCPUSUM(JNUM)/ICALLS*1000._JPRB
        IF(ICALLS > 1 ) THEN
          ZSTDDEV = 1000._JPRB*&
           &SQRT((TIMESQSUM(JNUM)-TIMESUM(JNUM)**2/ICALLS)&
           &/(ICALLS-1))
        ELSE
          ZSTDDEV = _ZERO_
        ENDIF
        IF( LDETAILED_STATS .OR. NPROC_STATS==1) THEN
          WRITE(KULOUT,*) CCDESC(JNUM)
          WRITE(KULOUT,'(I3,1X,I5,6(1X,F9.1),2(1x,F4.1))')&
           &JNUM,ICALLS,ZSUM,ZAVE,ZAVETCPU,ZAVEVCPU,&
           &ZSTDDEV,ZMAX,ZSUMB,ZFRAC
        ENDIF
      ENDIF
    ENDDO
    ZCOMTIM = SUM(TIMESUM(51:99))+SUM(TIMESUM(151:199))
    ZDETAIL = SUM(TIMESUM(:))-TIMESUM(0)-TIMESUM(1)-SUM(TIMESUM(20:23))
    IF( LDETAILED_STATS .OR. NPROC_STATS==1) THEN
      WRITE(KULOUT,'((A,F8.1))')&
       &'PERCENT OF DETAIL CAPTURED OUT OF TOTAL ',&
       &ZDETAIL/TIMESUM(0)*100.0_JPRB
      WRITE(KULOUT,'(2(A,F8.1))')&
       &'TOTAL COMMUNICATION TIME=',ZCOMTIM,&
       &' AS PERCENT OF TOTAL=',ZCOMTIM/TIMESUM(0)*100.0_JPRB
      WRITE(KULOUT,'((A,2F8.1))')&
       &'CPU-TIME AND VECTOR CPU-TIME AS PERCENT OF TOTAL ',&
       &TTCPUSUM(0)/TIMESUM(0)*100.0_JPRB,TVCPUSUM(0)/TIMESUM(0)*100.0_JPRB
    ENDIF
  ENDDO
  WRITE(KULOUT,*) 'STATS FOR ALL PROCESSORS'
  WRITE(KULOUT,*) 'NUM, CALLS,MEAN(ms),MAX(ms),FRAC(%),UNBAL(%)'
  ZTOTUNBAL = _ZERO_
  DO JNUM=0,500
    IF(NCALLS(JNUM) > 1) THEN
      ICALLS = NCALLS(JNUM)/2
      ZMEAN = ZAVEAVE(JNUM)/NPROC_STATS
      ZTOTMEAN =ZMEAN*ICALLS/1000._JPRB
      ZMAX  = ZAVEMAX(JNUM)
      ZUNBAL= (ZMAX-ZMEAN)/ZMEAN*100._JPRB
      ZFRAC = ZTOTMEAN/ZTOTAL*100.0_JPRB
      ZTOTUNBAL = ZTOTUNBAL+(ZMAX-ZMEAN)*ICALLS/1000.0_JPRB
      WRITE(KULOUT,*) CCDESC(JNUM)
      WRITE(KULOUT,'(I4,1X,I5,4(1X,F9.1))')&
       &JNUM,ICALLS,ZMEAN,ZMAX,ZFRAC,ZUNBAL
    ENDIF
  ENDDO

IF(LSTATS_COMMS)THEN
  WRITE(KULOUT,*) ''
  WRITE(KULOUT,*) 'STATS FOR COMMUNICATIONS'
  WRITE(KULOUT,*)  &
 &' NUM ROUTINE               CALLS    MEAN(ms)  MAX(ms)   FRAC(%)  UNBAL(%)'
  T_SUM=0.0
  DO JNUM=501,1000
    IF(NCALLS(JNUM) > 1) THEN
      ICALLS = NCALLS(JNUM)/2
      ZMEAN = ZAVEAVE(JNUM)/NPROC_STATS
      T_SUM=T_SUM+ZMEAN*ICALLS/1000._JPRB
      ZTOTMEAN =ZMEAN*ICALLS/1000._JPRB
      ZMAX  = ZAVEMAX(JNUM)
      ZUNBAL= (ZMAX-ZMEAN)/ZMEAN*100._JPRB
      ZFRAC = ZTOTMEAN/ZTOTAL*100.0_JPRB
      ZTOTUNBAL = ZTOTUNBAL+(ZMAX-ZMEAN)*ICALLS/1000.0_JPRB
      WRITE(KULOUT,'(I4,1X,A20,1X,I5,4(1X,F9.1))')&
       &JNUM,CCDESC(JNUM),ICALLS,ZMEAN,ZMAX,ZFRAC,ZUNBAL
    ENDIF
  ENDDO

  WRITE(KULOUT,*) ''
  WRITE(KULOUT,'(A,F10.1,A)')'SUMMED TIME IN COMMUNICATIONS = ',T_SUM, ' SECONDS '
  WRITE(KULOUT,*) ''

ENDIF
IF(LSTATS_OMP)THEN
  WRITE(KULOUT,*) ''
  WRITE(KULOUT,*) 'STATS FOR PARALLEL REGIONS'
  WRITE(KULOUT,*)  &
 &' NUM ROUTINE               CALLS    MEAN(ms)  MAX(ms)   FRAC(%)  UNBAL(%)'
  T_SUM=0.0
  DO JNUM=1001,JPMAXSTAT
    IF(NCALLS(JNUM) > 1) THEN
      ICALLS = NCALLS(JNUM)/2
      ZMEAN = ZAVEAVE(JNUM)/NPROC_STATS
      T_SUM=T_SUM+ZMEAN*ICALLS/1000._JPRB
      ZTOTMEAN =ZMEAN*ICALLS/1000._JPRB
      ZMAX  = ZAVEMAX(JNUM)
      ZUNBAL= (ZMAX-ZMEAN)/ZMEAN*100._JPRB
      ZFRAC = ZTOTMEAN/ZTOTAL*100.0_JPRB
      ZTOTUNBAL = ZTOTUNBAL+(ZMAX-ZMEAN)*ICALLS/1000.0_JPRB
      WRITE(KULOUT,'(I4,1X,A20,1X,I5,4(1X,F9.1))')&
       &JNUM,CCDESC(JNUM),ICALLS,ZMEAN,ZMAX,ZFRAC,ZUNBAL
    ENDIF
  ENDDO

  WRITE(KULOUT,*) ''
  WRITE(KULOUT,'(A,F10.1,A)')'SUMMED TIME IN PARALLEL REGIONS = ',T_SUM, ' SECONDS '
  WRITE(KULOUT,*) ''

ENDIF

  WRITE(KULOUT,'(A,F10.1,A,F4.1,A)')&
   &'TOTAL MEASURED IMBALANCE =',ZTOTUNBAL,&
   &' SECONDS, ',ZTOTUNBAL/ZTOTAL*100._JPRB,' PERCENT'
ELSE
  ZTOTAL=TIMESUM(0)
  ZTOTCPU = TTCPUSUM(0)
  ZTOTVCPU = TVCPUSUM(0)
ENDIF
WRITE(KULOUT,'(3(A,F10.1)/)')'TOTAL WALLCLOCK TIME ',ZTOTAL,&
 &' CPU TIME',ZTOTCPU,' VECTOR TIME ',ZTOTVCPU

!   Trace stats

IF (LTRACE_STATS) THEN
  WRITE(KULOUT,'(A)') '=== TRACE OF CALLS TO GSTATS'
  IF (NCALLS_TOTAL > NTRACE_STATS) THEN
    WRITE(KULOUT,'(A,2I10)') ' ONLY PART OF TRACE STORED AS BUFFER TO SMALL ',&
     & NCALLS_TOTAL,NTRACE_STATS
  ENDIF
  WRITE(KULOUT,'(A)') '==='
  CLACTION(0)='ON'
  CLACTION(1)='OFF'
  CLACTION(2)='SUSPEND'
  CLACTION(3)='RESUME'
  DO JCALL=1,MIN(NCALLS_TOTAL,NTRACE_STATS)
    ICALLER = MOD(NCALL_TRACE(JCALL),(JPMAXSTAT+1))
    IACTION   = NCALL_TRACE(JCALL)/(JPMAXSTAT+1)
    IF (IACTION == 0) THEN
      ZTIMELCALL(ICALLER) = TIME_TRACE(JCALL)
      ZTHISTIME(ICALLER) = _ZERO_
    ELSEIF (IACTION == 2) THEN
      ZTHISTIME(ICALLER) = TIME_TRACE(JCALL)-ZTIMELCALL(ICALLER)
    ELSEIF (IACTION == 3) THEN
      ZTIMELCALL(ICALLER) = TIME_TRACE(JCALL)
    ENDIF
    IF (IACTION == 1) THEN
      WRITE(KULOUT,'(1X,F10.3,1X,A,1X,A,1X,F10.3)') &
       &TIME_TRACE(JCALL),CCDESC(ICALLER),CLACTION(IACTION),&
       &ZTHISTIME(ICALLER)+(TIME_TRACE(JCALL)-ZTIMELCALL(ICALLER))
    ELSE
      WRITE(KULOUT,'(1X,F10.3,1X,A,1X,A)') TIME_TRACE(JCALL),CCDESC(ICALLER),&
       & CLACTION(IACTION)
    ENDIF
  ENDDO
ENDIF

IF(LSTATS .AND. MYPROC_STATS == 1) THEN
  PAVEAVE(0:KLEN) = ZAVEAVE(0:KLEN)
ELSE
  PAVEAVE(0:KLEN) = _ZERO_
ENDIF

WRITE(KULOUT,'(/A)')'===-=== END   OF TIMING STATISTICS ===-==='

RETURN
END SUBROUTINE GSTATS_PRINT
