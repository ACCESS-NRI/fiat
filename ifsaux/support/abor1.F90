! (C) Copyright 2005- ECMWF.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

SUBROUTINE ABOR1(CDTEXT)

USE PARKIND_FAUX  ,ONLY : JPIM
USE YOMLUN_FAUX, ONLY : NULOUT,NULERR
USE MPL_MODULE, ONLY : MPL_ABORT, MPL_RANK, MPL_NUMPROC
USE SDL_MOD, ONLY : SDL_TRACEBACK, SDL_SRLABORT
#ifdef NAG
USE F90_UNIX_IO, ONLY: FLUSH
#endif

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CDTEXT
INTEGER(KIND=JPIM) :: ILEN

ILEN=LEN(CDTEXT)
IF (NULOUT >= 0) WRITE(NULOUT,'(1X,A)') 'ABOR1 CALLED'

IF(ILEN <= 512) THEN
  IF (NULOUT >= 0) WRITE(NULOUT,'(1X,A)') CDTEXT
  IF (NULERR >= 0) WRITE(NULERR,'(1X,A,1X,I3,1X,A)') 'ABORT! ',MPL_RANK,CDTEXT
ELSE
  IF (NULERR >= 0) WRITE(NULERR,'(1X,A,1X,I3,1X,A)') 'ABORT! ',MPL_RANK,&
   & 'ABOR1 CALLED WITHOUT TEXT STRING'  
ENDIF

IF (NULOUT >= 0) THEN
  CALL FLUSH(NULOUT)
  IF (NULOUT /= 0 .AND. NULOUT /= 6) CLOSE(NULOUT)
ENDIF

CALL BREXIT(CDTEXT)

END SUBROUTINE ABOR1

SUBROUTINE BREXIT(CDTEXT)
USE MPL_MODULE, ONLY : MPL_ABORT, MPL_NUMPROC
USE SDL_MOD, ONLY : SDL_TRACEBACK, SDL_SRLABORT
CHARACTER(LEN=*), INTENT(IN) :: CDTEXT
! Added Arpege/Ifs collaboration for 01-Apr-2019
! Brexit means Brexit !
IF (MPL_NUMPROC > 1) THEN
  IF(LEN(CDTEXT) <= 512) THEN
    CALL MPL_ABORT(CDTEXT)
  ELSE
    CALL MPL_ABORT
  ENDIF
ELSE
  CALL SDL_TRACEBACK
  CALL FLUSH(0)
  CALL SDL_SRLABORT
ENDIF
END SUBROUTINE BREXIT

SUBROUTINE ABOR1FL(CDFILE, KLINENUM, CDTEXT)
USE PARKIND_FAUX  ,ONLY : JPIM
USE YOMLUN_FAUX, ONLY : NULOUT,NULERR
#ifdef NAG
USE F90_UNIX_IO, ONLY: FLUSH
#endif
IMPLICIT NONE
CHARACTER(LEN=*), INTENT(IN) :: CDFILE,CDTEXT
INTEGER(KIND=JPIM), INTENT(IN) :: KLINENUM
IF (LEN(CDFILE) > 0 .AND. KLINENUM > 0 .AND. NULERR >= 0) THEN
 1000 FORMAT(1X,A,A,":",I6.6)
  WRITE(NULERR,1000) 'ABOR1FL HAS BEEN CALLED AT ',CDFILE,KLINENUM
  CALL FLUSH(NULERR)
ENDIF
CALL ABOR1(CDTEXT)
END SUBROUTINE ABOR1FL


SUBROUTINE ABOR1_EXCEPTION_HANDLER()
  !! This routine, when registered as the fckit exception handler, will be called
  !! whenever any C++ exception is thrown. The exception is intercepted and can
  !! be inquired through the variable FCKIT_EXCEPTION.
  !! The exception can then also be thrown by:
  !!    CALL FCKIT_EXCEPTION%ABORT("I have my reasons")

#ifdef WITH_FCKIT
USE FCKIT_MODULE, ONLY : FCKIT_EXCEPTION
IF( FCKIT_EXCEPTION%LOCATION%IS_SET() ) then 
  CALL ABOR1FL( FCKIT_EXCEPTION%LOCATION%FILE(), FCKIT_EXCEPTION%LOCATION%LINE(), FCKIT_EXCEPTION%WHAT() )
ELSE
  CALL ABOR1( FCKIT_EXCEPTION%WHAT() )
ENDIF
#else
CALL ABOR1( "An unknown exception is handled via ABOR1_EXCEPTION_HANDLER. Compile faux with fckit to get a better error message" )
#endif
END SUBROUTINE
