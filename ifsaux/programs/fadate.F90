SUBROUTINE FADATE

USE PARKIND1, ONLY : JPIM, JPRB
USE XRD_GETOPTIONS

IMPLICIT NONE

#include "abor1.intfb.h"

INTEGER (KIND=JPIM), PARAMETER :: ILUN = 77
CHARACTER (LEN=*),   PARAMETER :: CLCA = 'CADRE.STANDARD'

INTEGER (KIND=JPIM), POINTER :: IDATES (:) 
LOGICAL :: LLGET
CHARACTER (LEN=256) :: CLF
INTEGER (KIND=JPIM) :: IREP, INBARP, INBARI
INTEGER (KIND=JPIM) :: IDATEG (22)

CALL INITOPTIONS (KOPTMIN=1)

CALL GETOPTION ('--fa-file', CLF, MND = .TRUE., USE = "FA file")
LLGET = .FALSE.
CALL GETOPTION ('--get', LLGET)
IDATES => NULL ()
CALL GETOPTION ('--set', IDATES)

CALL CHECKOPTIONS ()

INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CLF), 'OLD', .TRUE., &
           & .FALSE., 0_JPIM, INBARP, INBARI, CLCA)

IF (LLGET) THEN
  CALL FADIEX (IREP, ILUN, IDATEG)
  WRITE (*, *) IDATEG ( 1:11)
  WRITE (*, *) IDATEG (12:22)
ENDIF

IF (ASSOCIATED (IDATES)) THEN

  SELECT CASE (SIZE (IDATES))
    CASE (11)
      CALL FANDAR (IREP, ILUN, IDATES)
    CASE (22)
      CALL FANDAX (IREP, ILUN, IDATES)
    CASE DEFAULT
      CALL ABOR1 ('FADATE: A DATE IS AN ARRAY OF 11 OR 22 ELEMENTS')
  END SELECT

ENDIF

CALL FAIRME (IREP, ILUN, 'KEEP')

END SUBROUTINE FADATE
